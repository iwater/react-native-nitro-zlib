apply plugin: 'com.android.library'
apply plugin: 'org.jetbrains.kotlin.android'

def getExtOrDefault(name, defaultValue) {
    return rootProject.ext.has(name) ? rootProject.ext.get(name) : defaultValue
}

android {
    namespace "com.margelo.nitro.nitro_zlib"

    ndkVersion getExtOrDefault("ndkVersion", "26.1.10909125")
    compileSdkVersion getExtOrDefault('compileSdkVersion', 35)

    defaultConfig {
        minSdkVersion getExtOrDefault('minSdkVersion', 21)
        targetSdkVersion getExtOrDefault('targetSdkVersion', 35)
        buildConfigField "boolean", "IS_NEW_ARCHITECTURE_ENABLED", "false" // Default to false, app will override if needed

        externalNativeBuild {
            cmake {
                cppFlags "-fexceptions", "-frtti", "-std=c++20"
                arguments "-DANDROID_STL=c++_shared"
            }
        }
    }

    buildFeatures {
        buildConfig true
        prefab true
    }
    
    sourceSets {
      main {
        jniLibs.srcDirs += ['libs']
      }
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    packagingOptions {
        excludes += [
            "**/libc++_shared.so",
            "**/libjsi.so",
            "**/libreactnativejni.so",
            "**/libreact_nativemodule_core.so",
            "**/libturbomodulejsijni.so"
        ]
    }
}

dependencies {
    implementation "com.facebook.react:react-native:+"
    implementation project(':react-native-nitro-modules')
}

// Apply the Nitrogen-generated Gradle file
apply from: "../nitrogen/generated/android/RNZlib+autolinking.gradle"
